class PolymorphismTest {
  method run(): String = {
    var shape : Shape;
    var unit : Int;
    
    shape = new Shape().init("blue");
    unit = self.displayShape(shape);
    
    shape = new Rectangle().initRectangle("red", 2, 4);
    unit = self.displayShape(shape);

    shape = new Square().initSquare("yellow", 2);
    unit = self.displayShape(shape);

    shape = new Circle().init("green");
    unit = self.displayShape(shape);
    
    println("---------------------");
//    unit = shape.drawTwice(); //should give compilation error
    unit = new Circle().initCircle("").drawTwice();
    
    println("---------------------");
    unit = new ReferenceTest().run();

    "done"
  }
  
  method displayShape(shape : Shape) : Int = {
    var text : String;
    
    text = "I am a " + shape.getColor();
    text = text + " ";
    text = text + shape.getName();
    println(text);
    shape.draw()
  }
}

class ReferenceTest {
  method run(): Int = {
    var unit : Int;
    
    var bool1 : Bool;
    var bool2 : Bool;
    var int1 : Int;
    var int2 : Int;
    var intArr1 : Int[];
    var intArr2 : Int[];
    var string1 : String;
    var string2 : String;
    var shape1 : Shape;
    var shape2 : Shape;
    
    bool1 = true;
    bool2 = false;
    unit = self.swapBool(bool1, bool2);
    if(bool1) 
      println("bool1: true")
    else
      println("bool1: false");
    if(bool2) 
      println("bool2: true")
    else
      println("bool2: false");
    
    int1 = 1;
    int2 = 2;
    unit = self.swapInt(int1, int2);
    println("int1: " + int1);
    println("int2: " + int2);

    intArr1 = new Int[1];
    intArr2 = new Int[2];
    unit = self.swapIntArr(intArr1, intArr2);
    println("intArr1: " + intArr1.length);
    println("intArr2: " + intArr2.length);
    intArr1[0] = 1;
    intArr2[0] = 2;
    unit = self.swapIntArrValue(intArr1, intArr2);
    println("intArr1val: " + intArr1[0]);
    println("intArr2val: " + intArr2[0]);

    string1 = "1";
    string2 = "2";
    unit = self.swapString(string1, string2);
    println("string1: " + string1);
    println("string2: " + string2);
    unit = self.concatStringValue(string1, string2);
    println("string1val: " + string1);
    println("string2val: " + string2);
    
    shape1 = new Shape().init("1");
    shape2 = new Shape().init("2");
    unit = self.swapShape(shape1, shape2);
    println("shape1: " + shape1.getColor());
    println("shape2: " + shape2.getColor());
    unit = self.resetShape(shape1, shape2);
    println("shape1new: " + shape1.getColor());
    println("shape2set: " + shape2.getColor());

    0
  }

  method swapBool(n1 : Bool, n2 : Bool) : Int = {
    var temp : Bool;
    temp = n1;
    n1 = n2;
    n2 = temp;
    0
  }

  method swapInt(n1 : Int, n2 : Int) : Int = {
    var temp : Int;
    temp = n1;
    n1 = n2;
    n2 = temp;
    0
  }

  method swapIntArr(n1 : Int[], n2 : Int[]) : Int = {
    var temp : Int[];
    temp = n1;
    n1 = n2;
    n2 = temp;
    0
  }

  method swapIntArrValue(n1 : Int[], n2 : Int[]) : Int = {
    var temp : Int;
    temp = n1[0];
    n1[0] = n2[0];
    n2[0] = temp;
    0
  }

  method swapString(n1 : String, n2 : String) : Int = {
    var temp : String;
    temp = n1;
    n1 = n2;
    n2 = temp;
    0
  }

  method concatStringValue(n1 : String, n2 : String) : Int = {
    n1 = n1 + n2;
    0
  }
  
  method swapShape(n1 : Shape, n2 : Shape) : Int = {
    var temp : Shape;
    temp = n1;
    n1 = n2;
    n2 = temp;
    0
  }

  method resetShape(n1 : Shape, n2 : Shape) : Int = {
    var unit : Int;
    n1 = new Shape().init("3");
    unit = n2.setColor("3");
    0
  }
}

class Shape {
  var color : String;
  
  method init(col: String) : Shape = {
    color = col;
    self
  }
  
  method getName() : String = {
    "shape"
  }
  
  method draw() : Int = {
    println("~");
    0
  }
  
  method getColor() : String = {
    color
  }
  
  method setColor(col: String) : Int = {
    color = col;
    0
  }
}

class Rectangle <: Shape {
  var x : Int;
  var y : Int;
  
  method initRectangle(c: String, a: Int, b: Int) : Rectangle = {
    var unit: Shape;
    x = a;
    y = b;
    unit = self.init(c);
    self
  }
  
  method getName() : String = {
    "rectangle"
  }
  
  method draw() : Int = {
    var first: String;
    var second: String;
    var i: Int;
    i = 0;    
    first = " ";
    second = "|";
    while(i < x) {
      first = first + "-";
      second = second + " ";
      i = i + 1
    };
    second = second + "|";

    println(first);
    i = 0;
    while(i < y) {
      println(second);
      i = i + 1
    };
    println(first);    
    0
  }
}

class Square <: Rectangle {
  method initSquare(c: String, side: Int) : Square = {
    var unit: Shape;
    unit = self.initRectangle(c, side, side);
    self
  }

  method getName() : String = {
    "square"
  }
}

class Circle <: Shape {
  method initCircle(c: String) : Circle = {
    var unit: Shape;
    unit = self.init(c);
    self
  }
  
  method getName() : String = {
    "circle"
  }
  
  method draw() : Int = {
    println("O");    
    0
  }
  
  method drawTwice() : Int = {
    self.draw() + self.draw()
  }
}

method main() : Unit = {
  println("Polymorphism and Reference test suite " + new PolymorphismTest().run())
}
